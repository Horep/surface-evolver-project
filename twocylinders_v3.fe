// Angle between cylinder normals, should be between 0 and pi/2
parameter cyl_angle_degree = 0
#define cyl_angle (cyl_angle_degree*pi/180)
// Length of cylinders in 3d space
parameter cyl_len = 5
// Radius of the upper, lower and droplet cylinders
parameter rad_upper = 0.8
parameter rad_lower = 0.8
parameter rad_center = 0.8
// Volume of the droplet
parameter VOL = 0.1
// Contact angle, in degrees because why not
parameter cont_angle = 90
PARAMETER min_area = 0.002
PARAMETER min_lent = 0.05
PARAMETER ref_length = 10*min_lent
PARAMETER ref_area = 25*min_area

// The height should be greater than rad_lower+rad_upper
parameter height = 2
PARAMETER GZ = 1
#define DENS 1

#define tension_val (cos(cont_angle*pi/180)) 

// Lower Cylinder - display
constraint low_disp_con
formula: sqrt(y^2 + (height+z)^2) = rad_lower

// Upper Cylinder - display
constraint up_disp_con
formula: sqrt(z^2 + (y*cos(cyl_angle) - x*sin(cyl_angle))^2) = rad_upper

// Keep rotation cylinder ends flat
constraint up_disp_con_plane_back
formula: x*cos(cyl_angle) + y*sin(cyl_angle) = cyl_len

constraint up_disp_con_plane_front
formula: x*cos(cyl_angle) + y*sin(cyl_angle) = -cyl_len

// Define common/long terms for the upper energy
#define denom_u (z^2 + (x*(sin(cyl_angle))^2 - y*sin(2*cyl_angle)/2)^2 + (y*(cos(cyl_angle))^2 - x*sin(2*cyl_angle)/2)^2) // upper cylinder denominator
#define common_u ((tension_val*rad_upper/2)/denom_u) // common front of w for upper
#define com_grav (GZ*DENS / 2)
#define uproot (sqrt(rad_upper^2 - (y*cos(cyl_angle) - x*sin(cyl_angle))^2))
// Upper cylinder with energy
constraint up_energy
formula: sqrt(z^2 + (y*cos(cyl_angle) - x*sin(cyl_angle))^2) = rad_upper
energy:
e1: -common_u*(-2*y*z*(sin(cyl_angle))^2 - x*z*sin(2*cyl_angle)) -com_grav*y*((y * cos(cyl_angle))^2 /3 + (x*sin(cyl_angle))^2)
e2: -common_u*(2*x*z*(cos(cyl_angle))^2 + y*z*sin(2*cyl_angle)) -com_grav*x*(rad_upper^2 + x*y*sin(2*cyl_angle)/2)
e3: -common_u*(-2*x*y*cos(2*cyl_angle)+ (x^2 - y^2)*sin(2*cyl_angle))

content:
c1: -0.5*((rad_upper^2 / cos(cyl_angle)) * atan((y*cos(cyl_angle) - x*sin(cyl_angle))/uproot) + (y-x*tan(cyl_angle))*uproot)
c2: 0
c3: 0


#define logthing (log(height^2 / ((height + z)^2 + y^2))) // the weird log thing
#define arctanthing (atan((z*(height+z)+y^2)/(height*y)) - atan(z/y)) // the arctangent component
#define common_L (tension_val*rad_lower*height / (y^2 + z^2)^2) // prefactor of square brackets
#define common_L_front (tension_val*rad_lower / (y^2 + z^2)) // prefactor of thing before square brackets

// Lower cylinder with energy
constraint low_energy
formula: sqrt(y^2 + (height+z)^2)= rad_lower
energy:
e1: 0
e2: -tension_val*rad_lower*x*(z+height) / (y^2+(z+height)^2) 
e3: tension_val*rad_lower*x*y / (y^2+(z+height)^2)

content:
c1: 0
c2: height*x - x*sqrt(rad_lower^2 - y^2)
c3: 0

// named quantity for arbitrary direction gravity on facets
quantity arb_grav energy method facet_vector_integral global
vector_integrand:
q1: 0
q2: 0
q3: -DENS*GZ*z^2
// Now the specification
PARAMETER tan_num =tan(cyl_angle/2)
vertices
// Lower cylinder display
1 -cyl_len 0 -rad_lower-height constraint low_disp_con FIXED
2 -cyl_len rad_lower -height constraint low_disp_con FIXED
3 -cyl_len 0 rad_lower-height constraint low_disp_con FIXED
4 -cyl_len -rad_lower -height constraint low_disp_con FIXED
5  cyl_len -rad_lower -height constraint low_disp_con FIXED
6  cyl_len 0 rad_lower-height constraint low_disp_con FIXED
7  cyl_len rad_lower -height constraint low_disp_con FIXED
8  cyl_len 0 -rad_lower-height constraint low_disp_con FIXED

// Upper cylinder display
9 -cyl_len*cos(cyl_angle) -cyl_len*sin(cyl_angle) -rad_upper constraint up_disp_con up_disp_con_plane_back FIXED
10 (-(-cyl_len)*tan_num^2 - 2*(rad_upper)*tan_num + (-cyl_len))/(tan_num^2 + 1) (2*(-cyl_len)*tan_num - (rad_upper)*tan_num^2 + (rad_upper))/(tan_num^2 + 1) 0 constraint up_disp_con up_disp_con_plane_back FIXED
11 -cyl_len*cos(cyl_angle) -cyl_len*sin(cyl_angle) rad_upper constraint up_disp_con up_disp_con_plane_back FIXED
12 (-(-cyl_len)*tan_num^2 - 2*(-rad_upper)*tan_num + (-cyl_len))/(tan_num^2 + 1) (2*(-cyl_len)*tan_num - (-rad_upper)*tan_num^2 + (-rad_upper))/(tan_num^2 + 1) 0 constraint up_disp_con up_disp_con_plane_back FIXED
13 (-(cyl_len)*tan_num^2 - 2*(-rad_upper)*tan_num + (cyl_len))/(tan_num^2 + 1) (2*(cyl_len)*tan_num - (-rad_upper)*tan_num^2 + (-rad_upper))/(tan_num^2 + 1) 0 constraint up_disp_con up_disp_con_plane_front FIXED
14 cyl_len*cos(cyl_angle) cyl_len*sin(cyl_angle) rad_upper constraint up_disp_con up_disp_con_plane_front FIXED
15 (-(cyl_len)*tan_num^2 - 2*(rad_upper)*tan_num + (cyl_len))/(tan_num^2 + 1) (2*(cyl_len)*tan_num - (rad_upper)*tan_num^2 + (rad_upper))/(tan_num^2 + 1) 0 constraint up_disp_con up_disp_con_plane_front FIXED
16 cyl_len*cos(cyl_angle) cyl_len*sin(cyl_angle) -rad_upper constraint up_disp_con up_disp_con_plane_front FIXED

// Droplet
17 -rad_center 0 rad_lower-height constraint low_energy
18 0 rad_center sqrt(rad_lower^2 -rad_center^2)-height constraint low_energy
19 rad_center 0 rad_lower-height constraint low_energy
20 0 -rad_center sqrt(rad_lower^2 -rad_center^2)-height constraint low_energy
21 0 -rad_center -sqrt(rad_upper^2 - (rad_center*cos(cyl_angle))^2) constraint up_energy
22 rad_center 0 -sqrt(rad_upper^2 - (rad_center*sin(cyl_angle))^2) constraint up_energy
23 0 rad_center -sqrt(rad_upper^2 - (rad_center*cos(cyl_angle))^2) constraint up_energy
24 -rad_center 0 -sqrt(rad_upper^2 - (rad_center*sin(cyl_angle))^2) constraint up_energy

edges
// Lower cylinder display
1  1 2 constraint low_disp_con FIXED
2  2 3 constraint low_disp_con FIXED
3  3 4 constraint low_disp_con FIXED
4  4 1 constraint low_disp_con FIXED
5  1 8 constraint low_disp_con FIXED
6  2 7 constraint low_disp_con FIXED
7  3 6 constraint low_disp_con FIXED
8  4 5 constraint low_disp_con FIXED
9  8 7 constraint low_disp_con FIXED
10 7 6 constraint low_disp_con FIXED
11 6 5 constraint low_disp_con FIXED
12 5 8 constraint low_disp_con FIXED

// Upper cylinder display
13 9 10 constraint up_disp_con up_disp_con_plane_back FIXED
14 10 11 constraint up_disp_con up_disp_con_plane_back FIXED
15 11 12 constraint up_disp_con up_disp_con_plane_back FIXED
16 12 9 constraint up_disp_con up_disp_con_plane_back FIXED
17 9 16 constraint up_disp_con FIXED
18 16 15 constraint up_disp_con FIXED
19 15 10 constraint up_disp_con FIXED
20 15 14 constraint up_disp_con FIXED
21 13 14 constraint up_disp_con up_disp_con_plane_front FIXED
22 16 13 constraint up_disp_con up_disp_con_plane_front FIXED
23 12 13 constraint up_disp_con up_disp_con_plane_front FIXED
24 14 11 constraint up_disp_con up_disp_con_plane_front FIXED

// Droplet
25 17 18 constraint low_energy
26 18 19 constraint low_energy
27 19 20 constraint low_energy
28 20 17 constraint low_energy
29 21 20
30 24 17
31 18 23
32 19 22
33 23 24 constraint up_energy
34 23 22 constraint up_energy
35 22 21 constraint up_energy
36 21 24 constraint up_energy

faces
// Lower cylinder display
1 9 -6 -1 5 constraint low_disp_con density 0 FIXED// right bottom
2 10 -7 -2 6 constraint low_disp_con density 0 FIXED// right top
3 -3 7 11 -8 constraint low_disp_con density 0 FIXED// left top
4 8 12 -5 -4 constraint low_disp_con density 0 FIXED// left bottom

// Upper cylinder display
5 20 24 -14 -19 constraint up_disp_con density 0 FIXED// right top
6 -15 -24 -21 -23 constraint up_disp_con density 0 FIXED// left top
7 23 -22 -17 -16 constraint up_disp_con density 0 FIXED// left bot
8 19 -13 17 18 constraint up_disp_con density 0 FIXED// right bot

// Droplet
9 31 33 30 25 // front right
10 29 28 -30 -36 // front left
11 27 -29 -35 -32 // back left
12 32 -34 -31 26 // back right
//13 -27 -26 -25 -28 constraint low_disp_con tension 0// bot
//14 34 35 36 -33 constraint up_disp_con tension 0// top

bodies
1 -9 -10 -11 -12 volume VOL density DENS

// Other script commands to use during runtime
read

// change from linear mode to quadratic

// Making nicely refined cylinders and then freezing them so they don't refine more.
ref_cylinders := {
  set edge no_refine where not fixed;
  r; t 0.1; u; r;
  set facet no_refine where fixed;
  unset edge no_refine;
  set edge no_refine where fixed;
}

do_cols := {
foreach facet ff where on_constraint low_disp_con do set ff color red;
foreach facet ff where on_constraint up_disp_con do set ff color red;	
set body[1].facets color cyan;
}

calc := {
G 0;
ref_cylinders;
do_cols;
r 2;
 g 100; M 2; r; g 500; r; u; g 500;
}

showq;